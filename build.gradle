import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "maven"
    id "java"
}

group "me.dags"
version "0.4.0-SNAPSHOT"
def spongeAPI = "7.2.0"
description = "A markup syntax for Sponge"

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven { url "https://repo.spongepowered.org/maven"}
    maven { url "https://jitpack.io" }
}

dependencies {
    compile "org.spongepowered:spongeapi:${spongeAPI}"
    shade "com.github.dags-:Template:0.1.1"
    testCompile "junit:junit:4.12"
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate "me.dags.template.", "me.dags.text.template."
    classifier null
}

processResources {
    filter ReplaceTokens, tokens: [
            "id": project.name.toLowerCase(),
            "name": project.name,
            "version": project.version,
            "description": project.description,
            "spongeapi": "spongeapi@"+spongeAPI,
    ]
}

install.repositories.mavenInstaller.pom.whenConfigured { pom ->
    pom.dependencies.removeAll { it.scope == "compile" }
}

jar.dependsOn shadowJar
jar.enabled false